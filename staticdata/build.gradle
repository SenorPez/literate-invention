buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.6.RELEASE")
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
    }
}

plugins {
    id 'java'
    id 'jacoco'
    id 'idea'
    id 'org.springframework.boot' version '1.5.6.RELEASE'
    id 'org.asciidoctor.convert' version '1.5.6'
}

group 'com.senorpez.projectcars'
version '0.1-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

springBoot {
    executable = true
}

bootRun {
    if (project.hasProperty('args')) {
        args project.args.split('\\s+')
    }
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-hateoas:1.5.6.RELEASE'
    compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.7.0'
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.7.0'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'org.springframework:spring-test:4.3.10.RELEASE'
    testCompile 'org.springframework.boot:spring-boot-test:1.5.6.RELEASE'
    testCompile 'com.jayway.jsonpath:json-path-assert:2.4.0'
    testCompile group: 'com.jayway.restassured', name: 'json-schema-validator', version: '2.9.0'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.8.47'
    asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor:1.2.1.RELEASE'
    testCompile 'org.springframework.restdocs:spring-restdocs-core:1.2.1.RELEASE'
    testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc:1.2.1.RELEASE'
}

ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    inputs.dir snippetsDir
    dependsOn test
    attributes 'snippets': file('build/generated-snippets')
}

jar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = false
    }
}